
Test "Initial Exchange Rate"
    NewComptroller
    NewFToken ZRX fZRX initialExchangeRate:2e9
    Assert Equal (FToken fZRX ExchangeRateStored) (Exp 2e9)

Test "Initial Exchange Rate with Mint"
    NewComptroller
    ListedFToken ZRX fZRX initialExchangeRate:5e9
    -- Check initial exchange holds
    Invariant Remains (FToken fZRX ExchangeRateStored) (Exp 5e9)
    -- Mint some tokens and verify invariant still holds
    Prep Geoff Some ZRX fZRX
    Mint Geoff 50e18 fZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Bep20 fZRX TotalSupply) (Exactly 10e9)
    Assert Equal (Bep20 fZRX TokenBalance Geoff) (Exactly 10e9)
    -- Let's mint again and verify it's still good.
    Prep Torrey Some ZRX fZRX
    Mint Torrey 50e18 fZRX
    -- Also, verify token was minted at given exchange rate
    Assert Equal (Bep20 fZRX TotalSupply) (Exactly 20e9)
    Assert Equal (Bep20 fZRX TokenBalance Torrey) (Exactly 10e9)

Test "ZRX: Exch. Rate:2e9, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=18, Decimals Us=8 -> 2e9 Exchange Rate
    ListedFToken ZRX fZRX initialExchangeRate:2e9 decimals:8
    Assert Equal (FToken fZRX ExchangeRateStored) (Exp 2e9)
    -- Mint 50.0 ZRX at given exchange rate
    Prep Geoff Some ZRX fZRX
    Assert Equal (Bep20 fZRX TotalSupply) Zero
    Mint Geoff 50e18 fZRX
    -- 50e18 / 2e9 = 2.5e10
    Assert Equal (Bep20 fZRX TotalSupply) (Exactly 2.5e10)
    Assert Equal (Bep20 fZRX TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Bep20 ZRX Faucet fZRX 1.0e18
    Assert Equal (Bep20 ZRX TokenBalance fZRX) (Exactly 51.0e18)
    -- Mock total borrows
    FToken fZRX Mock totalBorrows 2.0e18
    Assert Equal (FToken fZRX TotalBorrows) (Exactly 2.0e18)
    -- Mock total reserves
    FToken fZRX Mock totalReserves 0.5e18
    Assert Equal (FToken fZRX Reserves) (Exactly 0.5e18)
    -- Okay, we're all set, let's check the exchange rate
    -- (51+2-0.5)e18/2.5e10 = 52.5e18/2.5e10 = 21e8 -> 21e26 (Exp)
    Assert Equal (FToken fZRX ExchangeRateStored) (Exp 2.1e9)

Test "USDC: Exch. Rate:2e-3, Cash(51e18) + Borrows(2.0e18) - Reserves(0.5e18) / Tokens(2.5e10)"
    NewComptroller
    -- Decimals You=6, Decimals Us=8 -> 2e-3 Exchange Rate
    ListedFToken USDC fUSDC initialExchangeRate:2e-3 decimals:8
    Assert Equal (FToken fUSDC ExchangeRateStored) (Exp 2e-3)
    -- Mint 50.0 USDC at given exchange rate
    Prep Geoff Little USDC fUSDC
    Assert Equal (Bep20 fUSDC TotalSupply) (Exactly 0.0)
    Mint Geoff 50e6 fUSDC
    -- 5.0e7 / 2e-3 = 2.5e10
    Assert Equal (Bep20 fUSDC TotalSupply) (Exactly 2.5e10)
    Assert Equal (Bep20 fUSDC TokenBalance Geoff) (Exactly 2.5e10)
    -- Set cash
    Bep20 USDC Faucet fUSDC 2.0e6
    Assert Equal (Bep20 USDC TokenBalance fUSDC) (Exactly 52.0e6)
    -- Mock total borrows
    FToken fUSDC Mock totalBorrows 5.0e6
    Assert Equal (FToken fUSDC TotalBorrows) (Exactly 5.0e6)
    -- Mock total reserves
    FToken fUSDC Mock totalReserves 1.5e6
    Assert Equal (FToken fUSDC Reserves) (Exactly 1.5e6)
    -- Okay, we're all set, let's check the exchange rate
    -- (52+5-1.5)e6/2.5e10 = 55.5e6/2.5e10 = 2.22e-3 -> 2.22e15 (Exp)
    Assert Equal (FToken fUSDC ExchangeRateStored) (Exp 2.22e-3)
